function version_str(ver){
    out =  ver[1]"."ver[2]"."ver[3]
    if (ver[4]) out = out "-"ver[4]
    return out
}
function print_ver_and_exit(git, cargo) {
    print "cargo version (" version_str(cargo) ") <= git version (" version_str(git) ")"
    exit(1)
}

function ver_cmp(v1, v2){
    if (v1[1] > v2[1]) return 1
    if (v1[1] < v2[1]) return -1

    if (v1[2] > v2[2]) return 1
    if (v1[2] < v2[2]) return -1

    if (v1[3] > v2[3]) return 1
    if (v1[3] < v2[3]) return -1

    return 0
}

BEGIN {
    split(git_ver, git, " ")
    split(cargo_ver, cargo, " ")

    if (!((length(cargo) == 3 || length(cargo) == 4) && (length(git) == 3   || length(git) == 4))) {
        print "Something went very wrong. Check versions in git and cargo"
        exit(2)
    }

    if (length(cargo) > length(git)) {
        if (ver_cmp(cargo, git) == 0)
            exit(0) # cargo and git version numbers match but cargo has a commit
        print "git tag was updated before updating cargo version"
        print_ver_and_exit(git, cargo)
    } else if (length(cargo) < length(git) || length(git) == 3) {
        cmp = ver_cmp(cargo, git)
        if (cmp == 0 && amend)
            exit(0)
        if (cmp == 1 && !amend)
            exit(0)
    } else {
        if (amend && cargo[4] == git[4])
            exit(0)
        if (!amend && cargo[4] == (git[4] + 1))
            exit(0)
    }

    print "Please update cargo version in accordance with git describe (do not include the hash)"
    print_ver_and_exit(git, cargo)
}
